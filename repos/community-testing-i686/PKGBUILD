# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software
pkgver=3.22.0+23+g9812711
pkgrel=1
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=(GPL2)
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop flatpak
         desktop-file-utils archlinux-appstream-data gtkspell3 libgudev)
makedepends=(intltool python docbook-xsl gnome-common git)
_commit=981271136fd0e6e14a0bffd04493be8d27c240de  # gnome-3-22
source=("git://git.gnome.org/gnome-software#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_SOFTWARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $pkgname
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgname
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
    --disable-static --disable-schemas-compile --disable-packagekit \
    --enable-flatpak --enable-gtk-doc
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname
  make DESTDIR="${pkgdir}" install
}

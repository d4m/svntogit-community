# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapps
pkgver=1.4.7
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('libgnomekbd')
makedepends=('meson' 'gobject-introspection' 'python-gobject' 'python2-gobject' 'vala')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('5124506d10d80fdbffcdba04083542c65e3e0ed4b967b12c3e1618de99db2409')
b2sums=('1f1d08b0cc1e97562dd525e4c006bedd3a0485ecc0d80d7b29a9ce95d8e9244c18c530c1ca8464b1f570d7960f30054309897442199cab3138dd7f7299b3d3e9')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr --buildtype=plain ..
    ninja
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" ninja install
    # rm useless scripts
    rm -r "${pkgdir}"/usr/bin

    # byte-compile python modules since meson does not implement autotools'
    # py-compile.
    # This is kind of ugly but removes traces of the build root.
    for _python in python3 python2; do
        while read -rd '' _file; do
            _destdir="$(dirname "${_file#${pkgdir}}")"
            ${_python} -m compileall -d "${_destdir}" "${_file}"
            ${_python} -O -m compileall -d "${_destdir}" "${_file}"
        done < <(find "${pkgdir}"/usr/lib/${_python}* -name '*.py' -print0)
    done
}

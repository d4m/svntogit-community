# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=libhandy
pkgver=0.0.5
pkgrel=2
pkgdesc="Library full of GTK+ widgets for mobile phones"
arch=(x86_64)
url="https://source.puri.sm/Librem5/libhandy"
license=(LGPL2.1)
depends=(gtk3)
makedepends=(git glade gobject-introspection meson vala)
_commit=9c99704e4cc02d8aff8c03ebd583197677f67e52  # tags/v0.0.5^0
source=("git+https://source.puri.sm/Librem5/libhandy.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build -Dgtk_doc=true
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" meson install -C build
  rm -r "$pkgdir"/usr/{bin,subdir}
}

# Maintainer: Sven-Hendrik Haase <svenstaro@gmail.com>

pkgname=terraform-provider-keycloak
pkgver=1.19.0
pkgrel=1
pkgdesc="Terraform provider for Keycloak"
url="https://github.com/mrparkers/terraform-provider-keycloak"
arch=("x86_64")
license=("MPL")
depends=("glibc")
makedepends=("go")
source=("$pkgname-$pkgver.tar.gz::https://github.com/mrparkers/terraform-provider-keycloak/archive/$pkgver.tar.gz")
sha256sums=('12cd336431366c82ac54005f9770dbeb525afd5c68e252043aaad9b87db4ee0b')

build() {
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath -mod=readonly -modcacherw"

  cd $pkgname-$pkgver
  go build .
}

package() {
  cd $pkgname-$pkgver
  install -Dm755 $pkgname "$pkgdir/usr/bin/$pkgname"
}

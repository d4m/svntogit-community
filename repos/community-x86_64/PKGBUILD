# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapps
pkgver=1.8.1
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('libdbusmenu-gtk3' 'libgnomekbd')
makedepends=('meson' 'samurai' 'gobject-introspection' 'python-gobject' 'vala')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('fa4dbe94d9c64621381f1fca89ec6113902931e4577a776e690548ee3f896720')
b2sums=('ad5b7d0d3637269de025bad1fcf79ee471353fc88f377c3f2777328b9337766c98b8bfb2acbf9ece40b874459fed55ad33f1167024a9cb66779245cc76a60f03')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
    # rm useless scripts
    rm -r "${pkgdir}"/usr/bin

    # byte-compile python modules since meson does not implement autotools'
    # py-compile.
    # This is kind of ugly but removes traces of the build root.
    while read -rd '' _file; do
        _destdir="$(dirname "${_file#${pkgdir}}")"
        python -m compileall -d "${_destdir}" "${_file}"
        python -O -m compileall -d "${_destdir}" "${_file}"
    done < <(find "${pkgdir}"/usr/lib/python3* -name '*.py' -print0)
}

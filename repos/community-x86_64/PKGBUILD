# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=kcm-fcitx5
pkgver=0.0.0.20200509
_commit=e0271577fedf96e1aa15922b538c67407d5401b9
pkgrel=1
pkgdesc="KDE Config Module for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx5"
license=('GPL')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'python')
source=("https://github.com/fcitx/kcm-fcitx5/archive/$_commit/kcm-fcitx5-$_commit.tar.gz")
sha512sums=('dc2c9ee743359ebea89cb1db4dd5774e9248801f146d34979f71ee000c2deff18ee4e0edbb6dd36ba3e0941ce59c24ce19f4dd9e437f69f746532d14fadc059d')

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}

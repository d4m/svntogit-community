# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=kcm-fcitx5
pkgver=0.0.0.20200520
_commit=176524f45d644e86cbb2453c489a750459d69f5a
pkgrel=1
pkgdesc="KDE Config Module for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx5"
license=('GPL')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'python')
source=("https://github.com/fcitx/kcm-fcitx5/archive/$_commit/kcm-fcitx5-$_commit.tar.gz")
sha512sums=('cdfb1f5f238c50f94e3885531941e2401641aa545ce045f62bbee2fb81581a48d4da95d0668eb916c62e37a030afd8deb7848e622a3635c2076af54352674c4c')

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}

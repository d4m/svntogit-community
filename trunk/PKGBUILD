# $Id$
# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>

pkgname=haskell-gtk
pkgver=0.14.6
pkgrel=1
pkgdesc="Binding to the gtk library for Gtk2Hs."
url="https://hackage.haskell.org/package/gtk3"
license=('LGPL2.1')
arch=('i686' 'x86_64')
depends=('ghc' 'gtk3' "haskell-pango" 'haskell-mtl' 'haskell-text')
makedepends=('haskell-gtk2hs-buildtools')
provides=('gtk2hs-gtk')
replaces=('gtk2hs-gtk')
conflicts=('gtk2hs-gtk')
source=(https://hackage.haskell.org/package/gtk3-$pkgver/gtk3-$pkgver.tar.gz)
sha256sums=('f4c0d3c51a5e06e5f6a8fcfc2a1303e0a3ed0242309fc6c1b9603be9de1f4258')

build() {
  cd "$srcdir"/gtk3-${pkgver}
  runhaskell Setup configure -O -p --enable-split-objs --enable-shared \
    --prefix=/usr --docdir=/usr/share/doc/haskell-gtk \
    --libsubdir=\$compiler/site-local/\$pkgid
  runhaskell Setup build
  runhaskell Setup haddock --hoogle --html
  runhaskell Setup register   --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

_update_deps() {
    _ver=$(pacman -Q $1 | cut -f2 -d\  | cut -f1 -d-)
    for i in $(seq 0 $(expr ${#depends[@]} - 1)); do
        if [ ${depends[$i]} == $1 ]; then
            depends[$i]="$1=${_ver}"
        fi
    done
}

package() {
  _update_deps ghc
  _update_deps haskell-mtl
  _update_deps haskell-pango

  cd "$srcdir"/gtk3-${pkgver}
  install -D -m744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -D -m744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  install -d -m755 "$pkgdir"/usr/share/doc/ghc/html/libraries
  ln -s /usr/share/doc/haskell-gtk/html "$pkgdir"/usr/share/doc/ghc/html/libraries/gtk2hs-gtk
  runhaskell Setup copy --destdir="$pkgdir"
}

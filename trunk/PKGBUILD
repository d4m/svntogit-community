# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Maintainer: Kyle Keen <keenerd@gmail.com>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrew Antle <andrew dot antle at gmail dot com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Chaiwat Suttipongsakul <cwt at bashell dot com>

pkgname=python2-markdown
_pkgbasename=Markdown
pkgver=3.1.1
pkgrel=5
pkgdesc="Python implementation of John Gruber's Markdown."
arch=('any')
url='https://python-markdown.github.io/'
license=('BSD')
depends=('python2-setuptools')
checkdepends=('python2-yaml')
source=("https://files.pythonhosted.org/packages/source/M/$_pkgbasename/$_pkgbasename-$pkgver.tar.gz")
md5sums=('d84732ecc65b3a1bff693d9d4c24277f')
b2sums=('9fd024f64f7aaa774a49f60e312e47bbc2ff0f368f083e4dfd0c38d1f12839dfcf3455f1d0c8cb0b9c851fe0d3a779c70784859f1682703bd6022d57b6548235')

build() {
    cd "$srcdir/$_pkgbasename-$pkgver"

    python2 setup.py build
}

check() {
    cd "$srcdir/$_pkgbasename-$pkgver"

    python2 -m unittest discover tests
}

package() {
    cd "$srcdir/$_pkgbasename-$pkgver"

    python2 setup.py install --root="$pkgdir" --optimize=0
    mv "$pkgdir/usr/bin/markdown_py" "$pkgdir/usr/bin/markdown_py2"
    install -Dm644 LICENSE.md "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=kcm-fcitx5
pkgver=0.0.0.20200603
_commit=85fbc01b26b02b56802c77aef0a86f71081f1cd0
pkgrel=1
pkgdesc="KDE Config Module for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx5"
license=('GPL')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'python')
source=("https://github.com/fcitx/kcm-fcitx5/archive/$_commit/kcm-fcitx5-$_commit.tar.gz")
sha512sums=('99943a1fa52d0c47bb9101f75a23e1800fbcfdf280e410999123aad48fe3653c1933e766421cbefbe7bfb51400122b12e539caa343cb3f4d82466c89dff202d0')

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}

# Maintainer: Lukas Fleischer <lfleischer@archlinux.org>
# Maintainer: Bruno Pagani <archange@archlinux.org>
# Contributor: Hugo Osvaldo Barrera <hugo@barrera.io>

pkgname=libasr
pkgver=1.0.3
pkgrel=1
pkgdesc="Free, simple and portable asynchronous resolver library."
arch=(x86_64)
url="https://www.opensmtpd.org/"
license=(BSD)
depends=(glibc)
#source=("https://www.opensmtpd.org/archives/$pkgname-$pkgver.tar.gz")
source=("https://github.com/OpenSMTPD/${pkgname}/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('9cd88e0172e6d426438875e09229d1d473d56db546d02b630f9dd14db226d68d')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENCE -t "${pkgdir}"/usr/share/licenses/${pkgname}/
  install -Dm644 README.md -t "${pkgdir}"/usr/share/doc/${pkgname}/
}

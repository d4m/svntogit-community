# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-usage
pkgver=3.28.0
pkgrel=1
pkgdesc="GNOME application to view information about use of system resources"
arch=(x86_64)
url="https://wiki.gnome.org/Apps/Usage"
license=(GPL3)
depends=(gtk3 libgtop)
makedepends=(git meson vala)
optdepends=('accountsservice: show user tags for processes')
groups=(gnome-extra)
_commit=b29adffaaf65aa6fcc3d403adbe7999f01157c89  # tags/v3.28.0^0
source=("git+https://gitlab.gnome.org/GNOME/gnome-usage.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^v//;s/-/+/g'
}

build() {
  arch-meson $pkgname build
  ninja -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
}

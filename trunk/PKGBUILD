# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=ubuntukylin-wallpapers
pkgver=20.04.1
pkgrel=1
pkgdesc="Ubuntu Kylin Wallpapers"
arch=('any')
license=('GPL')
url="https://github.com/UbuntuKylin/ubuntukylin-wallpapers"
groups=('ukui')
source=("$pkgname-$pkgver.tar.gz::https://github.com/UbuntuKylin/ubuntukylin-wallpapers/archive/v$pkgver.tar.gz")
sha512sums=('8a2136c25c06fbc3fefb682be76e76773d10801fa1b67a6783708b4dfe187148f0d288c8b01fb47ec934ab2d60324864560bb7500a44215bd0fbe0a9d67f2542')

package() {
  cd $pkgname-$pkgver
  install -Dm644 *.jpg *.png -t "$pkgdir"/usr/share/backgrounds/
  install -Dm644 ubuntukylin/* -t "$pkgdir"/usr/share/backgrounds/ubuntukylin/

  install -dm755 "$pkgdir"/usr/share/{gnome,ukui}-background-properties
  for _f in *.xml.in; do
    # TODO support i18n properly
    sed -e 's/_name>/name>/g' $_f > "$pkgdir"/usr/share/ukui-background-properties/${_f%.in}
    ln -s ../ukui-background-properties/${_f%.in} "$pkgdir"/usr/share/gnome-background-properties/
  done
}

# $Id: PKGBUILD 226039 2017-04-27 13:52:30Z felixonmars $
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: csslayer <wengxt AT gmail com>

pkgname=kcm-fcitx5
pkgver=0.0.0.20200526
_commit=410cbb1724f4b1733c94cabde3d2fc94d34632a1
pkgrel=1
pkgdesc="KDE Config Module for Fcitx5"
arch=('x86_64')
url="https://github.com/fcitx/kcm-fcitx5"
license=('GPL')
depends=('fcitx5-qt' 'kwidgetsaddons' 'kitemviews')
optdepends=('kdeclarative: for KCM support'
            'kirigami2: for KCM support')
makedepends=('extra-cmake-modules' 'kdeclarative' 'kirigami2' 'python')
source=("https://github.com/fcitx/kcm-fcitx5/archive/$_commit/kcm-fcitx5-$_commit.tar.gz")
sha512sums=('cc96b2cfbaec203eb3f80ff7e0d277aad7862c29df1d66a774649f43d94ef179a1a816b28dcbc4df48bce4e62a651af2f2bcdb6ab3614ab6acc021d00c1efad3')

build(){
  cd $pkgname-$_commit

  cmake -DCMAKE_INSTALL_PREFIX=/usr -DCMAKE_INSTALL_LIBDIR=/usr/lib -DKDE_INSTALL_USE_QT_SYS_PATHS=ON .
  make
}

package() {
  cd $pkgname-$_commit
  make DESTDIR="$pkgdir" install
}

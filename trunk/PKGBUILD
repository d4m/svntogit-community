# $Id$
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Yosef Or Boczko <yoseforb@gnome.org>

pkgname=gnome-software
pkgver=3.26.2
pkgrel=1
pkgdesc="GNOME Software Tools"
arch=(i686 x86_64)
license=(GPL2)
url="https://wiki.gnome.org/Apps/Software/"
depends=(gtk3 appstream-glib libsoup gsettings-desktop-schemas gnome-desktop flatpak
         packagekit archlinux-appstream-data gtkspell3 libgudev fwupd liboauth)
makedepends=(python docbook-xsl git valgrind meson gobject-introspection gtk-doc)
_commit=08dcacae605f2c71848eafe171bf950ca8cf371b  # tags/GNOME_SOFTWARE_3_26_2^0
source=("git+https://git.gnome.org/browse/gnome-software#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgname
  git describe --tags | sed 's/^GNOME_SOFTWARE_//;s/_/./g;s/-/+/g'
}

prepare() {
  mkdir build
  cd $pkgname
}

build() {
  cd build
  arch-meson ../$pkgname -Denable-rpm=false
  ninja
}

package() {
  cd build
  DESTDIR="$pkgdir" ninja install
}

# Maintainer: Jelle van der Waa <jelle@archlinux.org>

_name=enaml
pkgname=python-enaml
pkgver=0.10.4
pkgrel=1
pkgdesc='Declarative User Interfaces for Python'
arch=(x86_64)
license=('BSD' 'custom')
depends=(python-atom python-pyqt5 python-kiwisolver python-ply python-setuptools)
checkdepends=(python-pytest)
source=($pkgname-$pkgver::https://github.com/nucleic/enaml/archive/$pkgver.tar.gz)
sha512sums=('c31cd1849bfc35c92db469891553a66fbc1e56383ae6741042b3ec9f15647289cb9e88f62a9b96dfa42316464f3e2e41afc39e6e5e006b7a580bb294995f1f52')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

check() {
  cd ${_name}-${pkgver}
  local python_version=$(python -c 'import sys; print(".".join(map(str, sys.version_info[:2])))')

  PYTHONPATH="$PWD/build/lib.linux-$CARCH-${python_version}" pytest
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# $Id$
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapps
pkgver=1.0.4
pkgrel=2
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/xapps"
license=('GPL')
depends=('libgnomekbd' 'python')
makedepends=('gnome-common' 'gobject-introspection' 'python-gobject' 'python2-gobject')
options=(!emptydirs)
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('6578574561cb10b602c67669b0374b71d9cccd5d1c13c17d6a4b6ec2f8fe739a')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    mkdir -p build build-py2

    cd build
    ../configure --prefix=/usr \
                 --localstatedir=/var \
                 --libexecdir=/usr/lib/${pkgname}
    make

    cd ../build-py2
    PYTHON=python2 ../configure --prefix=/usr \
                                --localstatedir=/var \
                                --libexecdir=/usr/lib/${pkgname}
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}
    make -C build DESTDIR="${pkgdir}" install
    make -C build-py2/pygobject DESTDIR="${pkgdir}" install

    # Not worth fixing out-of-tree builds properly as git master has migrated to meson
    cp -Rv files/usr/share "${pkgdir}"/usr
}

# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Alex Filgueira <alexfilgueira (at antergos.com)>
# Contributor: Nate Simon <aurpkg (at natesimon.net)>

pkgname=xapps
pkgver=1.6.8
pkgrel=1
pkgdesc="Common library for X-Apps project"
arch=('x86_64')
url="https://github.com/linuxmint/${pkgname}"
license=('GPL')
depends=('libgnomekbd')
makedepends=('meson' 'samurai' 'gobject-introspection' 'python-gobject' 'python2-gobject' 'vala')
source=("${pkgname}-${pkgver}.tar.gz::${url}/archive/${pkgver}.tar.gz")
sha256sums=('c559203fd85da7f9d83b552ea840dfc11a3f380cdcc3d7f86c5ea6f6375d2a74')
b2sums=('c87d59b8ad5ff038d027a509ac395d919c1201d475d6aff024bbed3bb08dedf7174f6cbe34745ded69c6f8c57689a2c602c96e4156174f07fe579338e1e04d64')

build() {
    mkdir -p "${srcdir}"/${pkgname}-${pkgver}/build
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    meson --prefix=/usr \
          --libexecdir=lib \
          --buildtype=plain \
          ..
    samu
}

package(){
    cd "${srcdir}"/${pkgname}-${pkgver}/build

    DESTDIR="${pkgdir}" samu install
    # rm useless scripts
    rm -r "${pkgdir}"/usr/bin

    # byte-compile python modules since meson does not implement autotools'
    # py-compile.
    # This is kind of ugly but removes traces of the build root.
    for _python in python3 python2; do
        while read -rd '' _file; do
            _destdir="$(dirname "${_file#${pkgdir}}")"
            ${_python} -m compileall -d "${_destdir}" "${_file}"
            ${_python} -O -m compileall -d "${_destdir}" "${_file}"
        done < <(find "${pkgdir}"/usr/lib/${_python}* -name '*.py' -print0)
    done
}

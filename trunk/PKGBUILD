# Maintainer: David Runge <dvzrv@archlinux.org>

pkgname=argodejo
pkgver=0.1
pkgrel=1
pkgdesc="A music production session manager"
arch=('x86_64')
url="https://www.laborejo.org/argodejo/"
license=('GPL3')
groups=('pro-audio')
depends=('glibc' 'hicolor-icon-theme' 'new-session-manager' 'python'
'python-pyqt5' 'ttf-dejavu')
makedepends=('nuitka')
source=("https://laborejo.org/downloads/${pkgname}-${pkgver}.tar.gz"{,.sig})
sha512sums=('9b37cf1c16749016b908e0a0305ca8486f45a7ad219c5da314d33b908766e793bcf5116d25f769aa49811fb4f3b691ef6ee631a067c0adf9c3776b83223cc0d8'
            'SKIP')
b2sums=('16aea7d6bebb0745978153b99fa6faee6fcd7098899e4ad90a977ee6a9046f7685b207d611edeb17e6f4c3fd0a39bbb6ef9a4850e6df45ef0fde75a03ab5274f'
        'SKIP')
validpgpkeys=('45D952EF384ADFBB00913E3428C6A306F2909FEE') # Nils Hilbricht <nils@hilbricht.com>

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir/" install
}
